buildscript {
    repositories {
        mavenCentral()
    }
}

apply plugin: 'com.android.application'

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

android {
    // Robolectric needs this for NetworkMonitorTest
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    defaultConfig {
        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)

        versionName VERSION_NAME
        versionCode Integer.parseInt(VERSION_CODE)

        applicationId GROUP
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // Since we are not supporting instrument tests, use the sourceset for our unit tests
    sourceSets {
        androidTest.setRoot('src/test')
    }
}

// Required to resolve conflict between our version and AssertJ's transitive dependency. Try removing and re-compiling test-app in the future.
configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:22.2.0'
}

dependencies {
    compile project(':util')
    compile project(':util-otto')
    compile project(':util-otto-retrofit')
    compile project(':util-picasso')

    testCompile 'com.squareup.assertj:assertj-android:1.0.+'
    testCompile 'junit:junit:4.10'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.robolectric:robolectric:2.4'

    // These should mirror the testCompile dependencies above
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.+'
    androidTestCompile 'junit:junit:4.10'
    androidTestCompile 'org.mockito:mockito-all:1.9.5'
    androidTestCompile 'org.robolectric:robolectric:2.4'
}
